group 'me.nspain.cubtracking'
version '0.0.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.31'
    ext.ktor_version = '1.2.0'
    ext.solrj_version = '7.2.0'
    ext.logback_version = '1.2.3'
    ext.junit_version = '4.12'
    ext.config4k_version = '0.4.1'
    ext.spek_version = '2.0.4'
    ext.kluent_version = '1.49'
    ext.yavi_version = '0.2.2'
    ext.ktlint_version = '0.33.0'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "me.nspain.cubtracking.CubTrackingApplicationKt"

sourceSets {
    main.kotlin.srcDirs = ['src']
    main.resources.srcDirs = ['resources']

    test.kotlin.srcDirs = ['test/src']
    test.resources.srcDirs = ['test/resources']

    integrationTest.kotlin.srcDirs = ['integration-test/src']
    integrationTest.resources.srcDirs = ['integration-test/resources']

    integrationTest {
        kotlin {
            compileClasspath += main.compileClasspath + test.compileClasspath
            runtimeClasspath += main.runtimeClasspath + test.runtimeClasspath
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testIntegration
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    ktlint
}

sourceCompatibility = 1.8
compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/spekframework/spek/" }
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version

    ktlint group: 'com.pinterest', name: 'ktlint', version: ktlint_version

    // Ktor stuff
    implementation group: 'io.ktor', name: 'ktor-server-netty', version: ktor_version
    implementation group: 'io.ktor', name: 'ktor-gson', version: ktor_version
    implementation group: 'io.ktor', name: 'ktor-auth', version: ktor_version
    implementation group: 'io.ktor', name: 'ktor-auth-jwt', version: ktor_version
    implementation group: 'io.ktor', name: 'ktor-client-core', version: ktor_version
    implementation group: 'io.ktor', name: 'ktor-client-apache', version: ktor_version
    implementation group: 'io.ktor', name: 'ktor-locations', version: ktor_version

    // Config and logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: logback_version
    implementation group: 'io.github.config4k', name: 'config4k', version: config4k_version

    // Functionality
    implementation group: 'org.apache.solr', name: 'solr-solrj', version: solrj_version
    implementation group: 'am.ik.yavi', name: 'yavi', version: yavi_version

    // Testing
    testImplementation group: 'junit', name: 'junit', version: junit_version
    testImplementation group: 'org.spekframework.spek2', name: 'spek-dsl-jvm', version: spek_version
    testImplementation group: 'io.ktor', name: 'ktor-server-test-host', version: ktor_version
    testImplementation group: 'io.ktor', name: 'ktor-client-mock-jvm', version: ktor_version
    testImplementation group: 'io.ktor', name: 'ktor-server-tests', version: ktor_version
    testImplementation group: 'io.ktor', name: 'ktor-client-mock', version: ktor_version
    testImplementation group: 'io.ktor', name: 'ktor-client-mock-jvm', version: ktor_version
    testImplementation group: 'org.amshove.kluent', name: 'kluent', version: kluent_version

    testRuntimeOnly group: 'org.spekframework.spek2', name: 'spek-runner-junit5', version: spek_version
    testRuntimeOnly group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
}

shadowJar {
    baseName = 'cub-tracking-api'
    version = null
}

task integrationTest(type: Test) {
    description = 'Run integration tests'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style"
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
}